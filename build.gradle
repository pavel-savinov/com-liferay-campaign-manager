buildscript {
	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.defaults", version: "latest.release"
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.lang.merger", version: "latest.release"
		classpath group: "org.kt3k.gradle.plugin", name: "coveralls-gradle-plugin", version: "2.6.3"
	}

	repositories {
		mavenLocal()

		maven {
			url "http://nexus-wcm.liferay.org.es/repository/dxp"
		}

		maven {
			url "https://plugins.gradle.org/m2/"
		}
		

	}
}

apply plugin: "com.github.kt3k.coveralls"
apply plugin: "com.liferay.app.defaults.plugin"
apply plugin: "jacoco"

configure(subprojects.findAll {!it.childProjects}) {
	apply plugin: "com.liferay.defaults.plugin"
	apply plugin: "jacoco"

	jacocoTestReport {
		additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
		sourceDirectories = files(sourceSets.main.allSource.srcDirs)
		classDirectories =  files(sourceSets.main.output)

		reports {
			html.enabled = true
			xml.enabled = true
		}
	}
}

repositories {
	mavenLocal()
	jcenter()
}

task coverageReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
	additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
	classDirectories = files(subprojects.sourceSets.main.output)
	executionData = files(subprojects.jacocoTestReport.executionData)
	sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)

	reports {
		html.enabled = true
		xml.enabled = true
	}

	onlyIf = { true }

	doFirst {
		executionData = files(executionData.findAll { it.exists() })
	}
}

tasks.coveralls {
	dependsOn 'coverageReport'
}

coveralls.jacocoReportPath = "build/reports/jacoco/coverageReport/coverageReport.xml"
coveralls.sourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs).collect {
	file -> file.absolutePath
}

task checkstyle {
    ant.taskdef(resource: 'checkstyletask.properties') {
        classpath {
            fileset(dir: 'libs', includes: '*.jar')
        }
    }
    
    ant.checkstyle(config: 'checkstyle.xml', failOnViolation: false) {
        fileset(dir: 'campaign-manager-service/src')
        fileset(dir: 'campaign-manager-api/src')
        fileset(dir: 'campaign-manager-web/src')
        
        formatter(type: 'xml', tofile: 'checkstyle_report.xml')
    }
    
    ant.xslt(in: 'checkstyle_report.xml', out: 'checkstyle_report.html', style: 'checkstyle.xsl') {
	param(name: 'team-name', expression: 'Campaign Manager')
    }
}